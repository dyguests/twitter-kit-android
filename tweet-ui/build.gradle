/*
 * Copyright (C) 2015 Twitter, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        versionCode 1
        versionName version
        minSdkVersion rootProject.minSdkVersion
        consumerProguardFiles 'proguard.txt'
    }

    buildTypes {
        debug {
            buildConfigField "String", "GROUP", "\"${pom_group_id}\""
            buildConfigField "String", "ARTIFACT_ID", "\"${pom_artifact_id}\""
            buildConfigField "String", "BUILD_NUMBER", "\"${buildNumber}\""
            buildConfigField "long", "BUILD_TIME", "${System.currentTimeMillis()}L"

            testCoverageEnabled enableTestCodeCoverage
        }
        release {
            buildConfigField "String", "GROUP", "\"${pom_group_id}\""
            buildConfigField "String", "ARTIFACT_ID", "\"${pom_artifact_id}\""
            buildConfigField "String", "BUILD_NUMBER", "\"${buildNumber}\""
            buildConfigField "long", "BUILD_TIME", "${System.currentTimeMillis()}L"
        }
    }

    lintOptions {
        warningsAsErrors true
    }

    compileOptions {
        sourceCompatibility rootProject.sourceCompatibilityVersion
        targetCompatibility rootProject.targetCompatibilityVersion
    }

    // Forcibly add debug and the generated "R" files onto the test classpath
    sourceSets {
        test.java.srcDirs += "build/generated/source/r/debug"
        test.java.srcDirs += "src/debug"
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.includeAndroidResources true
    }
}

dependencies {
    // api project(':twitter-core')
    api 'com.ifun:twitter-core:3.4.0-20240103.125156-5'

    implementation dependency.picasso
    implementation dependency.supportCoreV4
    implementation dependency.supportCompatV4
    implementation dependency.recyclerview

    androidTestCompile dependency.retrofitMock
    androidTestCompile dependency.mockito
    androidTestCompile dependency.dexmakerMockito

    testCompile dependency.mockito
    testCompile dependency.junit
    testCompile(dependency.robolectric) {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
}

publishing {
    def publishVersion= "3.4.0-SNAPSHOT"

    publications {
        maven(MavenPublication) {
            groupId = 'com.ifun'
            artifactId = 'twitter-ui'
            version = publishVersion

            afterEvaluate {
                from components.release
            }
        }
    }

    def localProperties = new Properties()
    def localPropertiesFile = project.rootProject.file('local.properties')
    if (localPropertiesFile.exists()) {
        localProperties.load(localPropertiesFile.newDataInputStream())
    }

    repositories {
        maven {
            if (publishVersion.endsWith('SNAPSHOT')) {
                url 'http://192.168.0.12/nexus/repository/maven-snapshots/'
            } else {
                url 'http://192.168.0.12/nexus/repository/maven-releases/'
            }
            // allowInsecureProtocol true

            credentials {
                username = localProperties.getProperty("mavenUser")
                password = localProperties.getProperty("mavenPassword")
            }
        }
    }
}